#!/usr/bin/gawk -E

BEGIN{
  config()
  read_args()
  print "1. Loading data" > "/dev/stderr"
  load_data(NPS, N)
  load_data(ARCTOS, A)
  print "2. Comparing included Catalog #s" > "/dev/stderr"
  compare_catno()
  print "3. Comparing cells" > "/dev/stderr"
  compare_cells()
  
}


function config(  i, t) {
  PROCINFO["sorted_in"] =  "@ind_str_asc"
  RS = "\r?\n"
  FPAT = "([^,]*)|(\"([^\"]|\"\")+\")"
  OFS = "|"
  split("Accession #|Sci. Name:Genus|Sci. Name:Species|Collector|Collection #" \
        "|Collection Date|Other Numbers|Identified By|Ident Date|Lat LongN/W" \
        "|Elevation|Habitat|Reference Datum",t,"|")
  for (i in t)
    Tests[t[i]]
}

function read_args(    arg) {
  # Read arguments, check, give usage
  for (arg = 1; arg < ARGC; arg++) {
    if      (ARGV[arg] == "-a") {
      ARCTOS = ARGV[arg+1]
      arg++
    }
    else if (ARGV[arg] == "-n") {
      NPS = ARGV[arg+1]
      arg++
    }
    else if (ARGV[arg] == "-f")
      BYFIELD = 1
    else if ((ARGV[arg] == "-h") || (ARGV[arg] == "--help"))
      HELP = 1
    else
      error("Error: argument '" ARGV[arg] "' not recognized")
  }
  if (!ARCTOS || !NPS || HELP)
    error("Usage: nacompare \n         "\
          "-n NPS_CSV\n         "\
          "-a ARCTOS_CSV\n       "\
          "[ -f sort by field,record rather than by record,field (default) ]")
}

function load_data(file, array,     line, fn, i, c, nc) {
  
  while (getline < file) {
    if (!line++) {
      for (i = 1; i <= NF; i++) {
        fn[i] = clean($i)
        if (fn[i] == "Catalog #") {
          c = i
          nc++
        }
      }
      if (!c)
        error("  error: no 'Catalog #' field in '" file "'")
      if (nc>1)
        error("  error: more than one 'Catalog #' field in '" file "'")
    }
    else {
      # normalize catalog numbers, single space only
      gsub(/ +/," ",$c)
      if (isarray(array[clean($c)]))
        error("  error: duplicate Catalog # in '" file "' : " clean($c))
      for (i = 1; i <= NF ; i++)
        array[clean($c)][fn[i]] = clean($i)
    }
  }
  printf "  records in %-40s: %10'd\n", ("'" file "'"), line-1 > "/dev/stderr"
}

function compare_catno(    i, fail) {
  for (i in N)
    if (!(i in A)) {
      fail = 1
      print "  Catalog # '" i "' in NPS file is not in Arctos file" \
        > "/dev/stderr"
    }
  if (!fail)
    print "  all Catalog #s in NPS file included in Arctos file"    \
      > "/dev/stderr"
}

function compare_cells() {
  if (BYFIELD) {
    for (i in Tests)
      for (j in N)
        if ((j in A) && (i in N[j]))
          if (N[j][i] != A[j][i])
            print i, j, N[j][i] , A[j][i]
  }
  else {
    for (j in N)
      for (i in Tests)
        if ((j in A) && (i in N[j]))
          if (N[j][i] != A[j][i])
            print j, i, N[j][i] , A[j][i]
  }
}
  
function clean(t) {
  gsub(/^"/,"",t)
  gsub(/"$/,"",t)
  gsub(/""/,"\"",t)
  return t
}

function error(msg) {
  print msg > "/dev/stderr"
  exit 1
}
